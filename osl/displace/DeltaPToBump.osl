/*
 *  Copyright 2022 Laika, LLC. Authored by Mitch Prater.
 *
 *  Licensed under the Apache License Version 2.0 http://apache.org/licenses/LICENSE-2.0,
 *  or the MIT license http://opensource.org/licenses/MIT, at your option.
 *
 *  This program may not be copied, modified, or distributed except according to those terms.
 */
shader displace_DeltaPToBump
(
    string Notes = ""
    [[
        int connectable = 0,
        string help =
            "Converts a <b>DeltaP</b> (Vector Displacement) value into a shading "
            "<b>Normal</b> or a <b>SurfaceGradient</b> shading normal representation. "
    ]],

    vector DeltaP = vector(0.0)
    [[
        string label = "DeltaP",
        string help =
            "The <b>DeltaP</b> (Vector Displacement) value convert into a shading "
            "<b>Normal</b> or a <b>SurfaceGradient</b> shading normal representation. "
    ]],

    float Gain = 1.0
    [[
        string label = "Gain",
        int slider = 1, float slidermin = 0.0, float slidermax = 2.0,
        string help =
            "Controls how much bumped appearance the resulting "
            "shading <b>Normal</b> or <b>SurfaceGradient</b> will contain. "
    ]],

    output normal Normal = normal(0.0),
    output vector SurfaceGradient = vector(0.0)
)
{
    // Must reference P for auto-differentiation to work.
    point  Po = P;
    getattribute( "builtin", "Po", Po );

    // Compute the bumped normal.
    point  Pdisp = Po + DeltaP;
    normal Nbump = calculatenormal( Pdisp );

    // Mikkelsen, M. 2020
    // Surface Gradientâ€“Based Bump Mapping Framework
    // Journal of Computer Graphics Techniques, Vol. 9, No. 3
    // https://jcgt.org/published/0009/03/04/
    float  k = dot( N, Nbump );
    SurfaceGradient = ( k*N - Nbump )/max( 1.0e-10, abs(k) ) * Gain;

    Normal = normalize( N - SurfaceGradient );
}
